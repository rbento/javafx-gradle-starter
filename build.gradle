plugins {
  id "java"
  id "application"
  id "idea"
  alias libs.plugins.javafx.plugin
  alias libs.plugins.javafx.module
  alias libs.plugins.javafx.link
  alias libs.plugins.spotless
}

group = "rbento"
version = "0.0.1"

application {
  mainModule = "rbento"
  mainClass = "rbento.Program"
}

compileJava {
  dependsOn "spotlessApply"
}

dependencies {
  testImplementation libs.assertj.core
  testImplementation libs.junit.jupiter
  testRuntimeOnly libs.junit.jupiter.platform.launcher
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(20)
    vendor = JvmVendorSpec.AZUL
  }
}

javafx {
  version = "20.0.2"
  modules = ["javafx.controls", "javafx.fxml", "javafx.graphics"]
}

jlink {
  imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
  options = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"]
  launcher {
    name = "app"
  }
}

jlinkZip {
  group = "distribution"
}

repositories {
  mavenLocal()
  mavenCentral()
}

spotless {
  encoding "UTF-8"
  java {
    target "src/*/java/**/*.java"
    targetExclude "${layout.buildDirectory}/**"
    importOrder()
    removeUnusedImports()
    palantirJavaFormat()
    formatAnnotations()
    endWithNewline()
    licenseHeaderFile "${project.rootDir}/COPYRIGHT"
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events "PASSED", "FAILED", "SKIPPED"
  }
}
